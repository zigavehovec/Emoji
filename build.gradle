apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    espresso: '3.0.2',
    testRules: '1.0.2',
    kotlin: '1.3.50',
  ]

  ext.deps = [
    'kotlin': [
      'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
    ],
    'support': [
      'test': [
        'espresso': [
          'core': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
        ],
        'runner': "com.android.support.test:runner:${versions.testRules}",
        'rules': "com.android.support.test:rules:${versions.testRules}",
      ],
    ],
    'fastLaneScreenGrab': "tools.fastlane:screengrab:1.2.0",
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.19.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.15.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'

    classpath 'com.android.tools.build:gradle:3.5.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.24.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

codeQualityTools {
  lint {
    textReport = true
  }
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  ktlint {
    toolVersion = '0.34.2'
  }
  detekt {
    toolVersion = '1.0.1'
  }
  errorProne {
    toolVersion = '2.1.2'
  }
}

junitJacoco {
  // Don't care about sample and generated modules
  ignoreProjects = ['app', 'emoji-ios', 'emoji-google', 'emoji-twitter', 'emoji-compat', 'emoji-kotlin']
}

subprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }
}

ext {
  minSdkVersion = 15
  compileSdkVersion = 28
  targetSdkVersion = 28

  leakCanaryVersion = '2.0-beta-3'

  versionCode = VERSION_CODE
  versionName = VERSION_NAME

  supportAppCompat = "androidx.appcompat:appcompat:1.1.0"
  supportV4CoreUi = "androidx.legacy:legacy-support-core-ui:1.0.0"
  supportV4Compat = "androidx.core:core:1.0.0"
  supportRecyclerView = "androidx.recyclerview:recyclerview:1.0.0"
  supportCardView = "androidx.cardview:cardview:1.0.0"
  supportAnnotations = "androidx.annotation:annotation:1.0.0"
  supportEmoji = "androidx.emoji:emoji-appcompat:1.0.0"
  supportEmojiBundled = "androidx.emoji:emoji-bundled:1.0.0"

  leakCanaryDebug = "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

  testing = [
          junit             : 'junit:junit:4.13-beta-3',
          robolectric       : 'org.robolectric:robolectric:4.3',
          privateConstructor: 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0',
          assertJ           : 'org.assertj:assertj-core:2.9.1'
  ]

  javaVersion = JavaVersion.VERSION_1_7
}

wrapper {
  gradleVersion = '5.6.2'
  distributionType = Wrapper.DistributionType.ALL
}

subprojects {
  task allDependencies(type: DependencyReportTask) {}
}
